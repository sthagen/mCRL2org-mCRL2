% CHIRP: 11
% CAS: 30
% Frame: 40 (Header) + 16*N (Payload) + 24 (CRC), 0 <= N < 128
% NIT: Absolute minimum: bit @ 0.4µs, MT at 1µs, NIT at 2MT.
%
%

% Additional rules for if, to reduce the size of the linearised process.
var a, b: Bool;
eqn if(a, false, true) = !a;
    if(a, b, true) = a => b;
    if(a, b, false) = a && b;
    if(a, true, b) = a || b;

% User defined sorts
sort Sender = Nat; 
     Signal = struct NONE?isNone 
                   | NOISE?isNoise 
                   | DATA_BIT(s_: Sender)?isData 
                   | CAS_BIT?isCAS 
                   | FIRST_HEADER_BIT(s_: Sender)?isHeader 
                   | FIRST_BODY_BIT(s_: Sender)?isBody;
     Symbol = struct CHIRP  
                   | IDLE_END 
                   | CAS
                   | FRAME(s_: Sender)?isFrame
                   | FRAME_HEADER(s_: Sender)?isHeader 
                   | NOTHING;

% Model parameters
map deaf_node,            
    mute_node,            
    reset_node: Sender;   
    SLOT_length,
    MIN_DELAY,            
    MAX_DELAY: Nat;
    CHIRP_length: Nat;    
    ATTEMPTS,
    NIT_length,
    CYCLE_length: Pos;
    AP_offset: Nat;
    length: Symbol -> Nat;
    noise_max_burst,
    noise_min_backoff,
    noise_max_backoff: Nat;
var id: Sender;
eqn ATTEMPTS = 2;
    deaf_node = 2;      % Which is the deaf node (0 to disable)
    mute_node = 0;      % Which is the mute node (0 to disable)
    reset_node = 0;    % Which is the resetting node (0 to disable)
    MIN_DELAY = 33; % Min and max delay of delayed nodes
    MAX_DELAY = 33; 
    AP_offset = 6;
    SLOT_length = 24;
    CHIRP_length = 1;  % Lengths (in bits) of CHIRP, NIT, CAS, 
    NIT_length = 12;      %   frame header and frame
    length(CAS) = 3; 
    length(FRAME_HEADER(id)) = 6; 
    length(FRAME(id)) = 6 + 9; 
    length(NOTHING) = 0;
    noise_max_burst = 0;     % Noise model
    noise_min_backoff = 0;
    noise_max_backoff = 0;
    
% Model constants and helper functions
map NODES: Pos;
    FRM_START: Sender -> Nat;
    symbol: Signal -> Symbol;
    signal: Symbol -> Signal;
    noise_id: Nat;
var id: Sender;
eqn NODES = 3;   
    noise_id = NODES + 1;
    CYCLE_length = NODES * SLOT_length + NIT_length;
    FRM_START(id) = Int2Nat(id - 1) * SLOT_length + AP_offset;
    symbol(CAS_BIT) = CAS;
    symbol(FIRST_HEADER_BIT(id)) = FRAME_HEADER(id);
    symbol(FIRST_BODY_BIT(id)) = FRAME(id);
    symbol(NOISE) = NOTHING;
    symbol(NONE) = NOTHING;
    signal(CAS) = CAS_BIT;
    signal(FRAME_HEADER(id)) = FIRST_HEADER_BIT(id);
    signal(FRAME(id)) = FIRST_BODY_BIT(id);

act % Bus <--> Node
    get, get', put, put', Get, Put: Sender # Signal;
    % Barrier
    bus: Signal;
    bit, wait;
    Encode: Symbol;
    % MAC <--> CODEC 
    encode: Symbol;
    macCAS, macStart, macStop;
    % CODEC <--> POC
    decode, Decode: Sender # Symbol;
    is_idle, Is_idle: Bool;
    % Events (for debugging and visualisation purposes)
    enter_operation, init_sched, integrating, is_integrating, attempt_startup: Sender;
    abort: Nat;
    reset, Reset: Sender;

% The noise model is parameterised with three values: the maximum length of a 
% noise burst, during which the noise process generates noise. The minimum 
% backoff and maximum backoff bound the period in between bursts.
proc
  Noise = NoiseP(0, 0);
  NoiseP(burst, backoff: Nat) =
      ((backoff >= noise_min_backoff || burst > 0) && burst < noise_max_burst) 
      -> put(noise_id, NOISE) . 
         NoiseP(burst=burst + 1, backoff=0)
  + (backoff <= noise_max_backoff) 
      -> put(noise_id, NONE) . 
         NoiseP(burst=0, backoff=backoff + 1)
  + (burst == noise_max_burst) 
      -> put(noise_id, NONE) . 
         NoiseP(burst=0, backoff=1)
  + sum s: Signal . get(noise_id, s) . NoiseP();

% The bus process is modelled as a discrete-time process that reads from NODES 
% processes and writes the combined signal (two senders results in noise) back 
% to those processes.
map combine: Signal # Signal -> Signal;
var a, b: Signal;
eqn combine(NONE, b) = b;
    combine(a, NONE) = a;
    !(isNone(a) || isNone(b)) -> combine(a, b) = NOISE;
% Two variants are given: one that reads and writes in arbitrary order every bit
% period...
proc
  UnorderedBus(r, w: Set(Sender), nr, nw: Nat, s: Signal) =
  (nr <= NODES) -> (
    sum i: Sender, s': Signal . (!(i in r)) -> put'(i, s') . 
      UnorderedBus(r + {i}, w, nr + 1, nw, 
                   if(i == mute_node, s, combine(s, s')))
  ) <> (
    (nw <= NODES) -> (
      sum i: Sender . (!(i in w)) -> get'(i, if(i == deaf_node, NONE, s)) . 
        UnorderedBus(r, w + {i}, nr, nw + 1, s)
    ) <> (
      bus(s) . UnorderedBus({}, {}, 0, 0, NONE)
    )
  );
% ... and one that does it in a fixed order.
  OrderedBus(r, w: Sender, s: Signal) =
  (r <= NODES + 1) -> (
    sum s': Signal . put'(r, s') . 
      OrderedBus(r + 1, w, if(r == mute_node, s, combine(s, s')))
  ) <> (
    (w <= NODES + 1) -> (
      get'(w, if(w == deaf_node, NONE, s)) . OrderedBus(r, w + 1, s)
    ) <> (
      bus(s) . OrderedBus(1, 1, NONE)
    )
  );
% It should not make a difference which is used, so the UnorderedBus is only 
% there fore debugging purposes.
  Bus = OrderedBus(1, 1, NONE);

% The CODEC process models the SDL process of the same name. We abstract away
% from the process of bitstrobing. What remains is the mutually exclusive 
% sending and receiving of bit patterns.
proc CODEC(id: Sender) = CODEC'Init(id);
proc CODEC'Init(id: Sender) = CODEC'Receive(id, 0, 0, 0, NONE);
proc CODEC'Receive(id: Sender, lastsender: Sender, idle, data: Nat, sig: Signal) = is_idle(idle >= CHIRP_length) . CODEC'Receive(id, lastsender, idle, data, sig)
  + bit . CODEC'Receive(id, lastsender, idle, data, sig)
  + put(id, NONE) . CODEC'Receive(id, lastsender, idle, data, sig)
  + sum S: Symbol . encode(S) . CODEC'Send(id, signal(S), 0, length(S))
  + sum S: Signal . get(id, S) . 
    (
      isNone(S) -> CODEC'Receive(id, 0, if(isNone(sig), 
                           min(CHIRP_length, idle + 1), 1), 0, S)
    + isNoise(S) -> CODEC'Receive(id, 0, 0, 0, S)
    + isData(S) -> CODEC'Announce(id, s_(S), 0, if(lastsender == s_(S), data+1, 0), sig)
    + isCAS(S) -> CODEC'Announce(id, s_(S), 0, if(isCAS(sig), data + 1, 1), S)
    + (isHeader(S) || isBody(S)) -> CODEC'Announce(id, s_(S), 0, 1, S)
    );
proc CODEC'Announce(id: Sender, lastsender: Sender, idle, data: Nat, sig: Signal) = is_idle(false) . CODEC'Announce(id, lastsender, idle, data, sig)
  + (data > 0 && data == length(symbol(sig))) 
      -> decode(id, symbol(sig)) . 
         CODEC'Receive(id, lastsender, idle, data, if(isHeader(sig), FIRST_BODY_BIT(s_(sig)), sig))
      <> CODEC'Receive(id, lastsender, idle, data, sig);
proc CODEC'Send(id: Sender, sig: Signal, sent, len: Nat) = is_idle(false) . CODEC'Send(id, sig, sent, len)
  + bit . CODEC'Send(id, sig, sent, len)
  + (sent == 0) -> put(id, sig) . CODEC'Send(id, sig, 1, len)
  + (sent > 0 && sent < len) 
      -> put(id, if(isCAS(sig), sig, DATA_BIT(id))) . 
         CODEC'Send(id, sig, sent + 1, len)
  + (sent == len && isHeader(sig)) 
      -> put(id, DATA_BIT(id)) . 
         CODEC'Send(id, DATA_BIT(id), sent + 1, length(FRAME(id)))
  + (sent == len && !isHeader(sig)) 
      -> sum S: Signal . get(id, S) . CODEC'Receive(id, 0, 0, 0, NONE)
      <> sum S: Signal . get(id, S) . CODEC'Send(id, sig, sent, len);

% The MAC process models the SDL process of the same name, and decides when the
% CODEC process should send data. When it is active, it periodically sends a 
% (startup) frame. If activation is done by a macCAS action, a CAS symbol is 
% sent before activation. The MAC process is deactivated by a macStop action.
proc MAC(id: Sender, togo: Int, active: Bool) =
  macCAS . encode(CAS) . MAC(active=true, togo=FRM_START(id) + SLOT_length - 1)
+ macStart . MAC(active=true, togo=FRM_START(id))
+ macStop . MAC(active=false)
+ active -> (
    (togo > 0) -> (
      wait . MAC(togo=togo - 1)
    ) <> (
      encode(FRAME_HEADER(id)) . MAC(togo=CYCLE_length - 1)
    )
  ) <> wait . MAC();

% The DelayedController models a normal POC process that starts after MIN_DELAY 
% and before MAX_DELAY clock ticks.
proc DelayedController(id: Sender) = DelayedController'Off(id);
proc DelayedController'Off(id: Sender) = DelayedController'Wait(id, 0);
proc DelayedController'Wait(id: Sender, counter: Nat) = decode(id, CAS) . DelayedController'Wait(id, counter)
  + sum id': Sender . 
      decode(id, FRAME_HEADER(id')) . 
      DelayedController'Wait(id, counter)
  + sum id': Sender . 
      decode(id, FRAME(id')) . 
      DelayedController'Wait(id, counter)

  + (counter < MAX_DELAY) -> bit . DelayedController'Wait(id, counter + 1)
  + (counter >= MIN_DELAY) -> Controller(id, ATTEMPTS);

% The Controller process models the startup behaviour of the POC SDL process.  
% Its states correspond directly to the SDL states defined in the FlexRay 
% specification.
proc Controller(id: Sender, attempts: Nat) = Controller'StartupPrepare(id, attempts);
proc Controller'StartupPrepare(id: Sender, attempts: Nat) = (attempts > 1) -> attempt_startup(id) . Controller'ColdstartListen(id, attempts, 0, 0)
                   <> integrating(id) . Controller'IntegrationListen(id, attempts);
proc Controller'IntegrationListen(id: Sender, attempts: Nat) = decode(id, CAS) . Controller'IntegrationListen(id, attempts)
  + sum id': Sender . decode(id, FRAME_HEADER(id')) . Controller'IntegrationListen(id, attempts)
  + sum id': Sender . 
      decode(id, FRAME(id')) . 
      bit . 
      init_sched(id) . 
      Controller'InitialiseSchedule(id, attempts, 0, id')
  + reset(id) . Controller'AbortStartup(id, ATTEMPTS)

  + is_integrating(id) . Controller'IntegrationListen(id, attempts)

  + bit . Controller'IntegrationListen(id, attempts);
proc Controller'ColdstartListen(id: Sender, attempts: Nat, tStartup, tStartupNoise: Nat) = (
    decode(id, CAS) . Controller'ColdstartListen(id, attempts, tStartup, 0)
  + sum id': Sender . 
      decode(id, FRAME_HEADER(id')) . 
      Controller'ColdstartListen(id, attempts, tStartup, 0)
  + sum id': Sender . 
      decode(id, FRAME(id')) . 
      bit . 
      init_sched(id) . 
      Controller'InitialiseSchedule(id, attempts, 0, id')
  + reset(id) . Controller'AbortStartup(id, ATTEMPTS)

  + bit . 
    (
      (tStartup >= 2 * CYCLE_length - 1 || 
       tStartupNoise >= 4 * CYCLE_length - 1)
      -> ( is_idle(true) . 
           macCAS . 
           Controller'ColdstartCollisionResolution(id, Int2Nat(attempts-1), -SLOT_length)
         + is_idle(false) . 
           Controller'ColdstartListen(id, attempts, 0, tStartupNoise + 1)
         )
    + (tStartup < 2 * CYCLE_length - 1 && 
       tStartupNoise < 4 * CYCLE_length - 1)
      -> ( is_idle(true) . 
           Controller'ColdstartListen(id, attempts, tStartup + 1, tStartupNoise + 1)
         + is_idle(false) . 
           Controller'ColdstartListen(id, attempts, 0, tStartupNoise + 1)
         )
    )
  );
proc Controller'InitialiseSchedule(id: Sender, attempts: Nat, timer: Nat, syncon: Sender) = (
    decode(id, CAS) . Controller'InitialiseSchedule(id, attempts, timer, syncon)
  + sum id': Sender . 
      decode(id, FRAME_HEADER(id')) . 
      Controller'InitialiseSchedule(id, attempts, timer, syncon)
  + sum id': Sender . 
      decode(id, FRAME(id')) .
      (
        (id' == syncon) 
        -> ((timer == CYCLE_length - 1) 
            -> bit . 
               Controller'IntegrationColdstartCheck(id, attempts, Int2Nat(FRM_START(syncon) + length(FRAME(id'))), syncon, false, false, false, false)
            <> Controller'AbortStartup(id, attempts)
           ) 
        <> Controller'InitialiseSchedule(id, attempts, timer, syncon)
      )
  + reset(id) . Controller'AbortStartup(id, ATTEMPTS)
  
  + bit . 
    (timer > CYCLE_length) 
    -> Controller'AbortStartup(id, attempts) 
    <> Controller'InitialiseSchedule(id, attempts, timer + 1, syncon)
  );
proc Controller'ColdstartCollisionResolution(id: Sender, attempts: Nat, timer: Int) = (
    decode(id, CAS) . Controller'AbortStartup(id, attempts)
  + sum id': Sender . 
      decode(id, FRAME_HEADER(id')) . 
      Controller'AbortStartup(id, attempts)
  + sum id': Sender . 
      decode(id, FRAME(id')) . 
      Controller'ColdstartCollisionResolution(id, attempts, timer)
  + reset(id) . Controller'AbortStartup(id, ATTEMPTS)
  
  + bit . 
    (timer < CYCLE_length * 4) 
    -> Controller'ColdstartCollisionResolution(id, attempts, timer + 1)
    <> Controller'ColdstartConsistencyCheck(id, attempts, 0, false, false)
  );
proc Controller'ColdstartConsistencyCheck(id: Sender, attempts: Nat, timer: Nat, ok1, ok2: Bool) = (
    decode(id, CAS) .  Controller'ColdstartConsistencyCheck(id, attempts, timer, ok1, ok2)
  + sum id': Sender . 
      decode(id, FRAME_HEADER(id')) . 
      Controller'ColdstartConsistencyCheck(id, attempts, timer, ok1, ok2)
  + sum id': Sender . 
      decode(id, FRAME(id')) . 
      Controller'ColdstartConsistencyCheck(id, attempts, timer, true, timer >= CYCLE_length)
  + reset(id) . Controller'AbortStartup(id, ATTEMPTS)
  
  + (timer < CYCLE_length * 1 - 1) 
    -> bit . Controller'ColdstartConsistencyCheck(id, attempts, timer + 1, ok1, ok2)
  + (timer < CYCLE_length * 2 - 1 && timer >= CYCLE_length * 1 - 1)
    -> (ok1 -> bit . Controller'ColdstartConsistencyCheck(id, attempts, timer + 1, ok1, ok2)
            <> ((attempts > 0) -> macStop . 
                                  Controller'ColdstartGap(id, Int2Nat(attempts - 1), 0)
                               <> bit . Controller'AbortStartup(id, attempts)))
  + (timer >= CYCLE_length * 2 - 1)
    -> (ok2 -> bit . enter_operation(id) . Controller'NormalOperation(id, attempts)
            <> Controller'AbortStartup(id, attempts))
  );
proc Controller'ColdstartGap(id: Sender, attempts: Nat, timer: Nat) = (
    decode(id, CAS) .  Controller'AbortStartup(id, attempts)
  + sum id': Sender . 
      decode(id, FRAME_HEADER(id')) . 
      Controller'AbortStartup(id, attempts)
  + sum id': Sender . 
      decode(id, FRAME(id')) . 
      Controller'ColdstartGap(id, attempts, timer)
  + reset(id) . Controller'AbortStartup(id, ATTEMPTS)
  
  + (timer < CYCLE_length * 1)
    -> bit . Controller'ColdstartGap(id, attempts, timer + 1)
    <> macStart . 
       Controller'ColdstartCollisionResolution(id, attempts, 0)
  );
proc Controller'IntegrationColdstartCheck(id: Sender, attempts: Nat, timer: Nat, syncon: Sender, seen1, seen2, ok1, ok2: Bool) = (
    decode(id, CAS) .  Controller'IntegrationColdstartCheck(id, attempts, timer, syncon, seen1, seen2, ok1, ok2)
  + reset(id) . Controller'AbortStartup(id, ATTEMPTS)
  + sum id': Sender . 
      decode(id, FRAME_HEADER(id')) . 
      Controller'IntegrationColdstartCheck(id, attempts, timer, syncon, seen1, seen2, ok1, ok2)
  + sum id': Sender . 
      decode(id, FRAME(id')) . 
      Controller'IntegrationColdstartCheck(id, attempts, timer, syncon, timer >= 1 * CYCLE_length, timer >= 2 * CYCLE_length, seen1 || (id'==syncon && timer >= 1 * CYCLE_length), seen2 || (id'==syncon && timer >= 2 * CYCLE_length))

  + (timer < 1 * CYCLE_length - 1)
    -> bit . Controller'IntegrationColdstartCheck(id, attempts, timer + 1, syncon, seen1, seen2, ok1, ok2)
  + (timer < 2 * CYCLE_length - 1 && timer >= 1 * CYCLE_length - 1) 
    -> bit . Controller'IntegrationColdstartCheck(id, attempts, timer + 1, syncon, seen1, seen2, ok1, ok2)
  + (timer < 3 * CYCLE_length - 1 && timer >= 2 * CYCLE_length - 1) 
    -> (ok1 -> bit . Controller'IntegrationColdstartCheck(id, attempts, timer + 1, syncon, seen1, seen2, ok1, ok2)
            <> bit . Controller'AbortStartup(id, attempts))
  + (timer >= 3 * CYCLE_length - 1)
    -> (ok2 -> macStart . 
               attempt_startup(id) . Controller'ColdstartJoin(id, attempts, 0)
             <> Controller'AbortStartup(id, attempts))
  );
proc Controller'ColdstartJoin(id: Sender, attempts: Nat, timer: Nat) = (
    decode(id, CAS) .  Controller'ColdstartJoin(id, attempts, timer)
  + sum id': Sender . 
      decode(id, FRAME_HEADER(id')) . 
      Controller'ColdstartJoin(id, attempts, timer)
  + sum id': Sender . 
      decode(id, FRAME(id')) .  
      Controller'ColdstartJoin(id, attempts, timer)
  + reset(id) . Controller'AbortStartup(id, ATTEMPTS)
  
  + bit . (timer < 3 * CYCLE_length)
    -> Controller'ColdstartJoin(id, attempts, timer + 1)
    <> enter_operation(id) . Controller'NormalOperation(id, attempts)
  );
proc Controller'AbortStartup(id: Sender, attempts: Nat) = (
    decode(id, CAS) . Controller'AbortStartup(id, attempts)
  + sum id': Sender . 
      decode(id, FRAME_HEADER(id')) . 
      Controller'AbortStartup(id, attempts)
  + sum id': Sender . 
      decode(id, FRAME(id')) . 
      Controller'AbortStartup(id, attempts)
  + reset(id) . Controller'AbortStartup(id, ATTEMPTS)
  
  + macStop . abort(id) . Controller()
  );
proc Controller'NormalOperation(id: Sender, attempts: Nat) = bit . Controller'NormalOperation(id, attempts)
  + sum s: Symbol . decode(id, s) . Controller'NormalOperation(id, attempts)
  + reset(id) . Controller'AbortStartup(id, ATTEMPTS);


% An absent node is modelled explicitly to be able to reuse this specification
% for two-node scenarios without having to change the number of parallel 
% components.
proc AbsentNode(id: Sender) =
  wait|bit|bit . AbsentNode()
+ sum s: Signal . get(id, s) . AbsentNode()
+ put(id, NONE) . AbsentNode();

% A normal node consists of a CODEC, a MAC and a POC that starts immediately.
proc NormalNode(id: Sender) =
  allow({wait|bit|bit, Encode|bit, get, put, Decode, Is_idle, macCAS|macCAS, 
         macStart|macStart, macStop|macStop, enter_operation, integrating, 
         is_integrating, abort, init_sched, reset, attempt_startup},
    comm({encode|encode->Encode, decode|decode->Decode, 
          is_idle|is_idle->Is_idle},
      CODEC(id) || Controller(id, ATTEMPTS) || MAC(id, 0, false)
  ));

% A delayed node consists of a CODEC, a MAC and a POC that starts operation 
% after a bounded initial delay.
proc DelayedNode(id: Sender) =
  allow({wait|bit|bit, Encode|bit, get, put, Decode, Is_idle, macCAS|macCAS, 
         macStart|macStart, macStop|macStop, enter_operation, integrating, 
         is_integrating, abort, init_sched, reset, attempt_startup},
    comm({encode|encode->Encode, decode|decode->Decode, 
          is_idle|is_idle->Is_idle},
      CODEC(id) || DelayedController(id) || MAC(id, 0, false)
  ));

% If a resetting node is present, then this process controls when it may be 
% reset. For larger statespaces, this process can be changed to restrict the
% frequency with which a node can be reset.
proc Resetter =
  reset(reset_node) . Resetter()
+ bit . Resetter();    

% The system consists of three nodes. Each node is of type Absent, Normal or
% Delayed. The bus|... multi-actions form the clock tick barrier, all other
% actions are either a result of communication between subcomponents, or actions
% that signal relevant events such as failure to start up.
init 
  allow({Get, Put, 
         bus|Encode|Encode|Encode|bit|bit|bit , 
         bus|Encode|Encode|wait|bit|bit|bit |bit, 
         bus|Encode|wait|wait|bit|bit|bit |bit|bit, 
         bus|wait|wait|wait|bit|bit|bit |bit|bit|bit, 
         Decode, Is_idle, init_sched, 
         macCAS|macCAS, macStart|macStart, macStop|macStop, 
         enter_operation, is_integrating|is_integrating, integrating, abort, Reset, attempt_startup},
    comm({get|get'->Get, put|put'->Put, reset|reset->Reset},
      Bus || Noise || Resetter || 
      NormalNode(1) || DelayedNode(2) || NormalNode(3)
  ));

% Mappings for the mu-calculus formulae. 
map nextin: Nat # List(Nat) -> Sender;
    inornext: Nat # List(Nat) -> Sender;
    finite: Symbol -> Bool;
    nextsym: Symbol # List(Nat) -> Symbol;
    correct_nodes: Sender -> List(Sender);
    CORRECT_NODES: List(Sender);
    remove: List(Sender) # Sender -> List(Sender);
var s, s2: Sender;
    l: List(Sender);
    sym: Symbol;
eqn nextin(s, l) = inornext((s + 1) mod (NODES + 1), l);
    l != [] && s in l -> inornext(s, l) = s;
    l != [] && !(s in l) -> inornext(s, l) = nextin(s, l);
    finite(sym) = (isHeader(sym) || isFrame(sym)) => s_(sym) <= NODES;
    nextsym(FRAME_HEADER(s), l) = FRAME(s);
    nextsym(FRAME(s), l) = FRAME_HEADER(nextin(s, l));
    s <= NODES -> correct_nodes(s) = 
      if(!(s in {reset_node, deaf_node, mute_node}), 
         s |> correct_nodes(s + 1), 
         correct_nodes(s + 1));
    s > NODES -> correct_nodes(s) = [];
    CORRECT_NODES = correct_nodes(0);
    remove(s2 |> l, s) = if(s2 == s, l, s2 |> remove(l, s));
    remove([], s) = [];


